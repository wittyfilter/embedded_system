---
layout: post
title: Lab 04 Input/Output
date: 2023-09-01 00:00:00
---
<!-- more -->
##### This lab is mainly from the CP216 labs in [WLU](https://bohr.wlu.ca/cp216/labs)

The CPUlator: [https://cpulator.01xz.net/?sys=arm-de1soc](https://cpulator.01xz.net/?sys=arm-de1soc).


Assembly I/O
------------

On a 'real' DE1-SoC computer, we could connect to external devices such as keyboards and monitors, and communicate with this devices through its JTAG (named after the [Joint Test Action Group](https://en.wikipedia.org/wiki/JTAG) which codified it) UART ([Universal Asynchronous Receiver/Transmitter](https://en.wikipedia.org/wiki/Universal_asynchronous_receiver-transmitter)). The simulator cannot communicate with actual external devices or files, but it can simulate writing to and reading from a UART in its _JTAG UART_ box. The box gives the base address of the UART at `0xff201000`, and we use this address to communicate with the simulated UART.

* * *

Writing a String
----------------

The following code writes a text string to the UART:

```nasm
// Constants
.equ UART_BASE, 0xff201000     // UART base address
.org    0x1000    // Start at memory location 1000
.text  // Code Section
.global _start
_start:
// print a text string to the UART
ldr  r1, =UART_BASE
ldr  r4, =TEXT_strING

LOOP:
ldrb r0, [r4]    // load a single byte from the string
cmp  r0, #0
beq  _stop       // stop when the null character is found
str  r0, [r1]    // copy the character to the UART DATA field
add  r4, r4, #1  // move to next character in memory
b LOOP

_stop:
b    _stop

.data  // Data Section
// Define a null-terminated string
TEXT_strING:
.asciz    "This is a text string"

.end
```

When executed, this code places the null-terminated (by the use of the `.asciz` directive) string "This is a text string." into memory at location `0x1030`. Look at the contents of memory by clicking on the _Memory_ tab, and choose _Size: Byte_ in the _Settings_ box. The text is readable in memory:

String in Memory

![string constant in memory]({{ site.baseurl }}/public/images/mem_string_1.png)

Note that the string takes up only the memory actually assigned to it.

You should see the string output in the _UART_ box:

String in UART

![UART output]({{ site.baseurl }}/public/images/UART_output.png)

You can clear the contents of the UART (but not the queues) by clicking on the upper-left corner of the UART box and choosing _Clear terminal_ from the drop-down menu.

* * *

Reading a String
----------------

The following code reads a text string from the UART:

```nasm
// Constants
.equ UART_BASE, 0xff201000     // UART base address
.equ SIZE, 80        // Size of string buffer storage (bytes)
.equ VALID, 0x8000   // Valid data in UART mask
.org    0x1000       // Start at memory location 1000
.text           // Code section
.global _start
_start:

// read a string from the UART
ldr  r1, =UART_BASE
ldr  r4, =READ_strING
add  r5, r4, #SIZE // store address of end of buffer

LOOP:
ldr  r0, [r1]  // read the UART data register
tst  r0, #VALID    // check if there is new data
beq  _stop         // if no data, return 0
strb r0, [r4]      // store the character in memory
add  r4, r4, #1    // move to next byte in storage buffer
cmp  r4, r5        // end program if buffer full
beq  _stop
b    LOOP

_stop:
b    _stop

.data  // Data section
// Set aside storage for a string
READ_strING:
.space    SIZE

.end
```

Input in the _UART_ box shows up as ASCII code in the _Read FIFO:_ text field:

String in the UART Read FIFO

![UART input]({{ site.baseurl }}/public/images/UART_input.png)

Having read your input from the _UART_ box, the results should show up in memory:

New String in Memory

![string in buffer]({{ site.baseurl }}/public/images/mem_string_2.png)

Note that the unused portion of the buffer shows up as black dots, indicating that the memory has been set aside for storage, while unallocated memory shows up as red dots.

The UART Control Registers
--------------------------

The constant `VALID` is a byte _mask_ that control the use of the UART. The UART has two 32-bit control registers at memory location `0xff201000` (here named `UART_BASE` for readability). The control registers are mapped as:

![UART control register]({{ site.baseurl }}/public/images/UART_control.png)

Characters received by the UART are stored in a 64-byte (character) queue called the _read FIFO_. Characters written to the UART are stored in a 64-byte queue called the _write FIFO_. (Both can be seen at the bottom of the UART dialog box in the CPULator.)

Data received by the UART (typed by you in the CPULator, or received from an external source in the physical computer) is stored in the _read FIFO_. The _RAVAIL_ field contains the number of characters in the _read FIFO_, and the _RVALID_ bit is set to 1. The lines:

```nasm
.equ UART_BASE, 0xff201000 // UART base address
…
.equ VALID, 0x8000// Valid data in UART mask
…
ldr  r1, =UART_BASE
…
LOOP:
ldr  r0, [r1]      // read the UART data register
tst  r0, #VALID    // check if there is new data
beq  _stop         // if no data, return 0
…
```
  

determines if there is data in the _read FIFO_ by checking whether the _RVALID_ bit is set to 1. It does so by:

*   first loading the contents of the UART data register into `r0`. This is necessary because the UART data register is really just a location in memory, and ARM does not allow the direct comparison of values in memory - they must be loaded into a register.
*   `tst`\-ing the contents of the UART data register (now in `r0`) against the `VALID` mask. In hex this mask is `0x8000`, which is ‭`1000 0000 0000 0000‬` in binary. Note that the 15th bit of this value is set to 1 - this matches the location of the _RVALID_ bit in the UART data register. `tst`\-ing against this binary value returns a 0 if the 15th bit of the target is 0, and 1 if the 15 bit of the target is 1. The result of the `tst` sets appropriate flags to be used with the branch instructions, and then the result is thrown away. (For example, the `Z` (Zero) flag is set if a value is = 0, and 1 if not. The `beq` instruction looks at the `Z` flag to determine if a value is indeed 0.) Simply put, this is a technique to determine if _RVALID_ is true or false.
*   using the `beq` branch instruction to branch to `_stop` if _RVALID_ is false (0), and keep going if _RVALID_ is true (1).

The actual characters in the _read FIFO_ are copied into memory by the lines:

```nasm
.equ UART_BASE, 0xff201000 // UART base address
…
ldr  r1, =UART_BASE
ldr  r4, =TEXT_strING
…
strb r0, [r4]      // store the character in memory
add  r4, r4, #1    // move to next byte in storage buffer
…
```  

which do so by:

*   using the same line as above to copy the contents of the UART data register into `r0`.
*   copying this character now in `r0` to the memory location stored in `r4` with the line `strb r0, [r4]`. The `B` on the `str` instruction instructs ARM to copy only a single byte to memory, rather than the default 4-byte word. There is other data in the register beyond the right-most byte, but it is ignored.
*   moving to the next byte location in memory with the instruction `add r4, r4, #1`.

As each character is read from the _read FIFO_ queue, the 16 byte value in the _RAVAIL_ field is decremented by 1. When the _read FIFO_ queue has been fully read, _RAVAIL_ is set to 0, _RVALID_ field is set to 0, and the _DATA_ field is undefined.

* * *

Directives
----------

`.asciz`

- Defines a NUL-terminated (a 0x00 byte) string literal.

`.ascii`

- Defines a string literal with no NUL termination.

`.end`

- Defines the end of program. Optional, but recommended. Everything after this directive is ignored.

Instructions
------------

The instructions used to test registers: `tst`, and `and`.

`tst`

- Performs a bitwise `and` operation on the values in `rn` and `Operand2`. The result is _not_ applied to the register. The `N` (negative) and `Z` (zero) flags are updated according to the result.

- Formats:  
`tst rn, Operand2`

- `rn`

  - The register to test.

- `Operand2`

  - The mask to apply to the `rn`.

- The instruction:

          tst r0, #0x1
        

- ands the value stored in `r0` against the immediate value `0x1` (1 decimal). In essence it is testing to see whether the first bit of the register is 1 or 0. If the first bit of `r0` is 0, the `Z` flag is set to 1, otherwise it is set to 0. In both cases the `Z` flag is 0.

`and`

- Performs a bitwise `and` operation on the values in `rn` and `Operand2`, and writes the result to the register `rd`. The `N` (negative) and `Z` (zero) flags are updated according to the result only if the condition `S` is used.

- Formats:  
`and{cond} rd, rn, Operand2`

- `{cond}`

  - The optional condition to apply to the instruction. Using `S` updates the appropriate condition codes.

- `rd`

  - The destination register.

- `rn`

  - The register to and against.

- `Operand2`

  - The mask to apply to the `rn`.

- The instructions:

          and r0, r1, #0x1 
          andS r2, r3, #0xF
        

- The first line ands the value stored in `r1` against the immediate value `0x1` (1 decimal) and puts the result into `r0`. In essence `r0` will contain the values 0 or 1, depending on the original value in `r1`. The condition flags are not set.  
The second line ands the value stored in `r3` against the immediate value `0xF` (15 decimal) and puts the result into `r2`. In essence `r2` will contain only the first 4 bits of `r3`. The condition flags are set because of the use of the `S` condition on the instruction.

`tst` and `and` both apply a Boolean and against a value. `tst` does not preserve the result of that Boolean operation but it does update the appropriate flags, while `and` preserves the result of that Boolean operation and updates the flags only if the appropriate condition is set. Using `and` is a particularly useful way to extract specific bits of a register.

Task: 
-----------

1.  Using the following writing and reading codes as a basis, write a program named `l04_t01.s` that reads a string from the UART and writes that string back to the UART until the _Enter_ key is pressed. Do not use any memory. Hint: the _Enter_ key returns the hex value `0x0a`.
    
    What happens if you type in data without pressing the _Enter_ key and run the program?
    
    * * *

 
2.  Using the following writing and reading codes as a basis, write a program named `l04_t02.s` that reads a string from the UART and writes that string to memory until either the _Enter_ key is pressed or the memory buffer is full. Do not store the value of the _Enter_ key in memory.
    
    (Note that the memory buffer - the number of bytes you set aside in memory to store the data read from the UART - does not have to match the size of the UART FIFO queue buffers. The UART queue buffers are 64 bytes in size, but your memory buffer can be that size or larger or smaller.)
    
    What happens to the _Read FIFO:_ if all of its data is not read? Can you run the program again?
    
    * * *
    
```nasm
/*
-------------------------------------------------------
write_string.s
-------------------------------------------------------
Author:  David Brown
ID:      123456789
Email:   dbrown@wlu.ca
Date:    2023-07-31
-------------------------------------------------------
Writes a string to the UART
-------------------------------------------------------
*/
// Constants            
.equ UART_BASE, 0xff201000     // UART base address

.org 0x1000  // Start at memory location 1000
.text        // Code section
.global _start
_start:

// print a text string to the UART
ldr  r1, =UART_BASE
ldr  r4, =TEXT_strING
        
LOOP:
ldrb r0, [r4]    // load a single byte from the string
cmp  r0, #0
beq  _stop       // stop when the null character is found
str  r0, [r1]    // copy the character to the UART DATA field
add  r4, r4, #1  // move to next character in memory
b LOOP

_stop:
b _stop

.data  // Data Section
// Define a null-terminated string
TEXT_strING:
.asciz    "This is a text string"

.end
```

```nasm
/*
-------------------------------------------------------
read_string.s
-------------------------------------------------------
Author:  David Brown
ID:      123456789
Email:   dbrown@wlu.ca
Date:    2023-07-31
-------------------------------------------------------
Reads a string from the UART
-------------------------------------------------------
*/
// Constants
.equ UART_BASE, 0xff201000     // UART base address
.equ SIZE, 80        // Size of string buffer storage (bytes)
.equ VALID, 0x8000   // Valid data in UART mask

.org 0x1000  // Start at memory location 1000
.text        // Code section
.global _start
_start:

// read a string from the UART
ldr  r1, =UART_BASE
ldr  r4, =READ_strING
add  r5, r4, #SIZE // store address of end of buffer

LOOP:
ldr  r0, [r1]      // read the UART data register
tst  r0, #VALID    // check if there is new data
beq  _stop         // if no data, return 0
strb r0, [r4]      // store the character in memory
add  r4, r4, #1    // move to next byte in storage buffer
cmp  r4, r5        // end program if buffer full
beq  _stop
b    LOOP

_stop:
b _stop

.data  // Data section
// Set aside storage for a string
READ_strING:
.space    SIZE

.end
```

Zip your answers file into a file named _yourID\_l04.zip_. Upload this zip file to the Chaoxing Space.

* * *
