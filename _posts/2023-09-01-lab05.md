---
layout: post
title: Lab 05 Subroutine
date: 2023-09-01 00:00:00
---
<!-- more -->
##### This lab is mainly from the CP216 labs in [WLU](https://bohr.wlu.ca/cp216/labs)

The CPUlator: [https://cpulator.01xz.net/?sys=arm-de1soc](https://cpulator.01xz.net/?sys=arm-de1soc).

Defining a Subroutine
---------------------

In the [Input / Output lab](https://zjutcs.gitee.io/blog/lab04), you worked with programs that read from and wrote to the UART. That code worked with only one string at a time, but what if we had multiple strings? It would be useful to have the equivalent of a Python or Java function to take care of writing a string to the UART. ARM allows the creation of named _subroutines_, chunks of code that are the rough equivalent of functions.

The following subroutine prints a string to the UART:

```nasm
// Subroutine constants
.equ UART_BASE, 0xff201000     // UART base address

PrintString:
/*
-------------------------------------------------------
Prints a null terminated string with an ENTER at the end.
-------------------------------------------------------
Parameters:
  r4 - address of string
Uses:
  r0 - holds character to print
  r1 - address of UART
-------------------------------------------------------
*/
// preserve temporary registers
stmfd sp!, {r0, r1, r4}
ldr   r1, =UART_BASE     // get address of UART

psLOOP:
ldrb  r0, [r4], #1       // load a single byte from the string
cmp   r0, #0
beq   _PrintString       // stop when the null character is found
str   r0, [r1]           // copy the character to the UART DATA field
b     psLOOP
_PrintString:
// recover temporary registers
ldmfd sp!, {r0, r1, r4}
bx    lr                 // return from subroutine
```

and to call this subroutine we issue the instructions:

```nasm
ldr r4, =address of string
bl  PrintString
```
      
The main program is:

```nasm
ldr r4, =address of string
bl  PrintString
```
      

Download the full program: [`sub_demo.s`]({{ site.baseurl }}/public/sub_demo.s).

Note that our subroutines are placed after the `_stop B _stop` instruction, but before the `.data` section. This guarantees that subroutines will be not executed by mistake.

Loading a value into `r4` is the equivalent of passing a parameter - the subroutine documentation tells you that `r4` must contain the address of the string to print. Any register could be used for this, and there are more sophisticated methods for passing parameters to subroutines that will be covered in later labs.

The subroutine call is executed with the `bl` (_Branch Link_) instruction. Immediately before any line is executed, register `pc` (_Program Counter_), contains the address of the line. In this case, the `pc` contains the address `1004`, the address of the `bl` instruction:

![bl Instruction]({{ site.baseurl }}/public/images/bl.png)

and the `bl` instruction contains the address `1024`, the address of the `PrintString` subroutine.

When the `bl` instruction is executed, control of the program passes to the line labeled `PrintString`. The `pc` now contains the address of this line, in this case, `1024`. However, the program needs to be able to return to the line following the `bl` instruction once the subroutine has finished. The `L` (Link) part of the `bl` instruction instructs the processor to copy the address of the instruction following the `bl` into Register `lr` (_Link Register_). In this case the address `1008` is stored in `lr`:

![]({{ site.baseurl }}/public/images/printString.png)

It is very important that the subroutine do two things:

*   Preserve the value of any temporary registers it uses
*   Return the program counter to its proper value when finished

* * *

Preserving Registers
--------------------

Registers are preserved by pushing their values onto the system _stack_. The stack is an area of memory set aside for program use. A typical use is to push values onto it at the start of a subroutine and pop them off when finished. ARM provides instructions for pushing and popping multiple register values at once, which is far simpler than having to deal with the values on the stack one at a time.

The stack pointer is store in the register `sp` (_Stack Pointer_). The instructions to push / pop the stack are:

`stmfd` (_STore Memory Full Descending stack_)

- copies data from a list of registers to the stack

`ldmfd` (_LoaD Memory Full Descending stack_)

- copies data from the stack to a list of registers

If multiple registers are adjacent, they can be listed in the push and pop with a dash (`-`) representing the registers in between. For example:

```nasm
stmfd sp!, {r0, r3, r6}  // Pushes registers r0, r3, and r6
…
stmfd sp!, {r0-r4, lr}       // Pushes registers r0, r1, r2, r3, r4, and lr
```
      
Subroutines should thus be written in such a way that any registers that are not actually being used to return values should be preserved. This includes the stack pointer and the stack itself, which must be returned to their original states at the end of the subroutine.

The `PrintString` subroutine stores the values of `r0`, `r1`, and `r4` on the stack with the line:

        stmfd  sp!, {r0, r1, r4}
      

and the stack contains:

| Address        | Value | Description     |
|----------------|-------|-----------------|
| Call from main |       |                 |
| fffffff0       | 0     | contents of r0  |
| fffffff4       | 0     | contents of r1  |
| fffffff8       | 1058  | contents of r4  |
|                |       |                 |
| 00000000       | 0     | bottom of stack |

When finished, `r0`, `r1`, and `r4` need to be set back to their original values by being popped off the stack. The following line does this:

        // recover temporary registers 
        ldmfd sp!, {r0, r1, r4}
      

The `pc` register needs to contain the address of the that followed the `bl` subroutine call so that the program knows where to continue execution. The following line does this:

        bx    lr                 
        // return from subroutine
      

The `bx lr` (_Branch and eXchange)_ instruction copies the contents of `lr` to the `pc` register and continues program execution from that line.

Program execution then continues from the address in the `pc`.

This subroutine call process is not automated as it is in a high-level language like Python. It is up to you as the programmer to make sure the subroutine starts with the proper `stmfd` and ends with the proper `ldmfd` and that the `pc` register is properly reset with the `bx` instruction.

Forgetting to preserve a register may cause a _Function clobbered register(s):_ error. (Details are found by clicking on the _Details_ link that appears in the simulator _Messages_ dialog box.) Although this warning can be turned off, for this lab at least make sure that you fix the error, rather than ignore it!

For all subroutines fill in the registers uses in the `Parameters` and `Uses` sections as appropriate.

1.  Using the code from [`sub\_demo.s`]({{ site.baseurl }}/public/sub_demo.s)., update the `PrintString` subroutine so that it prints an end of line character at the end of the string it sends to the UART. (Reminder: the enter character is `0x0A`. Save the resulting program as `l05_t01.s`.
    
    * * *
    
2.  Using the code from [sub\_read.s]({{ site.baseurl }}/public/sub_read.s) and [read\_string.s]({{ site.baseurl }}/public/read_string.s), complete the subroutine `ReadString` that reads strings from the UART into the appropriate string storage. NOTE: you have to type four enter-terminated strings into the UART before running the program in order for it to work properly. Save the resulting program as `l05_t02.s`.
    
    * * *
    
3.  Write a program that combines the two previous tasks - it should read strings from the UART and write those strings both to memory and back to the UART. Save the resulting program as `l05_t03.s`. (Copy/paste is your friend.)
    
    * * *
    

Zip your answers file into a file named _yourID\_l05.zip_. Upload this zip file to the Chaoxing Space.

* * *